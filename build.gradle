plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.gradleup.shadow" version "9.2.2"
}

group = findProperty("group")
version = findProperty("version")

java {
    sourceCompatibility = JavaVersion.toVersion(findProperty("javaVersion"))
    targetCompatibility = JavaVersion.toVersion(findProperty("javaVersion"))
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven {
        url = "https://repo.thingsboard.io/artifactory/libs-release-public"
    }
}

dependencies {
    implementation 'org.thingsboard:rest-client:4.2.1'
}

shadowJar {
    archiveClassifier.set('')

    relocate 'org.thingsboard', 'org.shawn.shaded.tb'

    relocate 'com.auth0', 'com.example.shaded.auth0'
    relocate 'com.ethlo', 'com.example.shaded.ethlo'
    relocate 'com.fasterxml.jackson', 'org.shawn.shaded.jackson'
    relocate 'com.github.benmanes', 'com.example.shaded.caffeine'
    relocate 'oshi', 'com.example.shaded.oshi'
    relocate 'com.upokecenter', 'com.example.shaded.upokecenter'
    relocate 'javax', 'org.shawn.shaded.javax'
    relocate 'com.google', 'com.example.shaded.google'
    relocate 'com.networknt', 'com.example.shaded.networknt'
    relocate 'okio', 'com.example.shaded.okio'
    relocate 'com.squareup', 'com.example.shaded.squareup'
    relocate 'org.apache', 'org.shawn.shaded.apache'
    relocate 'dev.langchain4j', 'org.shawn.shaded.langchain4j'
    relocate 'io.micrometer', 'org.shawn.shaded.micrometer'
    relocate 'io.swagger', 'org.shawn.shaded.swagger'
    relocate 'jakarta', 'org.shawn.shaded.jakarta'
    relocate 'com.sun', 'org.shawn.shaded.sun'
    relocate 'net.objecthunter.exp4j', 'org.shawn.shaded.exp4j'
    relocate 'org.bouncycastle', 'org.shawn.shaded.bouncycastle'
    relocate 'org.checkerframework', 'org.shawn.shaded.checkerframework'
    relocate 'org.eclipse', 'org.shawn.shaded.eclipse'
    relocate 'org.springframework', 'org.shawn.shaded.springframework'
}

jar.enabled = false
tasks.build.dependsOn shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = null
            }
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/shawndenggh/thingsboard-rest-client-adapter")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
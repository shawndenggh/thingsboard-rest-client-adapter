plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.gradleup.shadow" version "9.2.2"
}

group = findProperty("group")
version = findProperty("version")

java {
    sourceCompatibility = JavaVersion.toVersion(findProperty("javaVersion"))
    targetCompatibility = JavaVersion.toVersion(findProperty("javaVersion"))
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven {
        url = "https://repo.thingsboard.io/artifactory/libs-release-public"
    }
}

dependencies {
    implementation 'org.thingsboard:rest-client:4.2.1'
}

shadowJar {
    archiveClassifier.set('')

    relocate 'org.thingsboard',        'org.shawn.shaded.tb'
    relocate 'com.fasterxml.jackson',  'org.shawn.shaded.jackson'
    relocate 'org.apache.http',        'org.shawn.shaded.http'
    relocate 'org.slf4j',              'org.shawn.shaded.slf4j'
    relocate 'org.bouncycastle',       'org.shawn.shaded.bouncycastle'
    relocate 'io.swagger',             'org.shawn.shaded.swagger'
    relocate 'com.google.common',      'org.shawn.shaded.guava'
    relocate 'javax.annotation',       'org.shawn.shaded.javax.annotation'
    relocate 'jakarta.annotation',     'org.shawn.shaded.jakarta.annotation'
}

jar.enabled = false
tasks.build.dependsOn shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(tasks.shadowJar) {
                classifier = null
            }
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/shawndenggh/thingsboard-rest-client-adapter")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}